apply plugin: 'android'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
    }
}

repositories {
    mavenCentral()
}

android {

    compileSdkVersion 19
    buildToolsVersion '20.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        androidTest.setRoot('src/test')
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    applicationVariants.all { variant ->
        // This is an annoying hack to get around the fact that the Gradle plugin does not support
        // having libraries with different minSdkVersions. Play Services has a min version of 9 (Gingerbread)
        // but Android Maps Utils supports 8 (Froyo) still
        variant.processManifest.doFirst {
            File manifestFile = file("${buildDir}/exploded-aar/com.google.maps.android/android-maps-utils/0.3/AndroidManifest.xml")
            if (manifestFile.exists()) {
                println("Replacing minSdkVersion in Android Maps Utils")
                String content = manifestFile.getText('UTF-8')
                content = content.replaceAll(/minSdkVersion="8"/, 'minSdkVersion=\"9\"')
                manifestFile.write(content, 'UTF-8')
                println(content)
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:19.1.+'
    compile 'com.google.android.gms:play-services:+'
    compile 'com.google.maps.android:android-maps-utils:0.3'
    compile 'com.octo.android.robospice:robospice-retrofit:1.4.9'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.google.android.gms:play-services:4.3.23'
    compile group:'com.squareup.picasso', name:'picasso', version:'2.2.0'
    compile 'com.jakewharton:butterknife:5.0.0'
    compile 'com.github.flavienlaurent.datetimepicker:library:0.0.1'

    compile fileTree(dir: 'libs', include: '*.jar')

    androidTestCompile 'junit:junit:4.11'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.1'
    androidTestCompile 'com.squareup:fest-android:1.0.+'
    androidTestCompile 'com.google.code.gson:gson:2.2.4'
}
